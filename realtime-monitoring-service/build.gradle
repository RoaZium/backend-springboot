import edu.sc.seis.launch4j.tasks.Launch4jLibraryTask

plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.1'
    id 'io.spring.dependency-management' version '1.1.5'
    id 'edu.sc.seis.launch4j' version '3.0.6'
}

group = 'com.company'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

jar {
    enabled = true
}

bootJar {
    enabled = true
}

launch4j {
    mainClassName = 'com.company.Main' // 메인 클래스 이름
    outputDir = "${buildDir}/DMS" // DMS 폴더에 직접 출력
    outfile = "DMSapi.exe" // exe 파일 이름
    headerType = 'console' // 콘솔 창 활성화
    dontWrapJar = true // JAR 파일을 포함하지 않음
}

tasks.register('createExeTask', Launch4jLibraryTask) {
    dependsOn tasks.named('bootJar')
}

tasks.named('build') {
    dependsOn tasks.named('createExeTask')
}

dependencies {
    // Spring Boot Starters
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'

    // Database
    implementation 'org.mariadb.jdbc:mariadb-java-client:3.3.2'
    implementation 'com.microsoft.sqlserver:mssql-jdbc'

    // Documentation & Utils
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.2.0'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.17.2'

    // Development Tools
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    // Testing
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
    useJUnitPlatform()
}
